# uncomment this and the last line for zprof info
# zmodload zsh/zprof

export DOTFILES="$HOME/.dotfiles"

[[ ! -f "${HOME}/.iterm2_shell_integration.zsh" ]] || source "${HOME}/.iterm2_shell_integration.zsh"

antidote_home=$DOTFILES/antidote
zhome=${ZDOTDIR:-$HOME}

if [[ ! $zhome/.zsh_plugins.zsh -nt $antidote_home/zsh_plugins.txt ]]; then
  [[ -e $zhome/.antidote ]] \
    || git clone --depth=1 https://github.com/mattmc3/antidote.git $zhome/.antidote
  [[ -e $antidote_home/zsh_plugins.txt ]] || touch $antidote_home/zsh_plugins.txt
  (
    source $zhome/.antidote/antidote.zsh
    antidote bundle <$antidote_home/zsh_plugins.txt >$zhome/.zsh_plugins.zsh
  )
fi

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# your default editor
export EDITOR='nano'
export VEDITOR='code'
export VISUAL='code'

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load everything but the completion
for file in ${config_files:#*/completion.zsh}; do
  source "$file"
done

autoload -Uz $zhome/.antidote/functions/antidote
autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
  compinit
done
compinit -C

eval "$(fnm env --use-on-cd)"
eval $(thefuck --alias)

# [ -f ~/.localrc ] && . ~/.localrc

[[ ! -f ~/.zsh_plugins.zsh ]] || source ~/.zsh_plugins.zsh

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh # To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.

unset config_files

# zprof